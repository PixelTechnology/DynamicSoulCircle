buildscript {
    ext {
        kotlin_version = "1.7.10"
        wcpelibVersion = "1.0.17"
    }
}

plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id 'maven-publish'
    id 'org.jetbrains.dokka' version "${kotlin_version}"
}


group = 'top.wcpe'
version = '1.0.2-alpha-dev-1'

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml']){
        expand props
    }
    from('src/main/java') {
        include '**/*.xml'
    }
    from('src/main/kotlin') {
        include '**/*.xml'
    }
}

repositories {
    mavenLocal()
    maven {
        url "http://maven.wcpe.top/repository/maven-public/"
        allowInsecureProtocol = true
    }
    mavenCentral()
}

dependencies {
    compileOnly 'org.spigotmc:spigot:1.12.2'
    compileOnly "top.wcpe:wcpelib-common:${wcpelibVersion}"
    compileOnly "top.wcpe:wcpelib-bukkit:${wcpelibVersion}"

    implementation 'eos.moe.dragoncore:DragonCore:2.4.7'

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains:annotations:23.0.0"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xjvm-default=all-compatibility"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xjvm-default=all-compatibility"
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            credentials {
                allowInsecureProtocol = true
                username = project.findProperty("username").toString()
                password = project.findProperty("password").toString()
            }
            def releasesRepoUrl = "http://maven.wcpe.top/repository/maven-releases/"
            def snapshotsRepoUrl = "http://maven.wcpe.top/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}